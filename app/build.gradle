/*
 * Rooster Mornings Android.
 * Copyright (c)  2017 Roosta Media. All rights reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-crash'

//https://kotlinlang.org/docs/reference/kapt.html
//https://github.com/realm/realm-java/issues/4650
kapt {
    generateStubs = true
}

android {
    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE")))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.roostermornings.android"
        minSdkVersion 17
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //http://stackoverflow.com/questions/35663413/xmlpullparserexception-binary-xml-file-line-17vector-tag-requires-viewportwid
        vectorDrawables.useSupportLibrary = true
        // Enabling multidex support.
        multiDexEnabled true
    }

    //As of AS 3.0
    //Learn more at https://d.android.com/r/tools/flavorDimensions-missing-error-message.html
    flavorDimensions "rooster-release"

    productFlavors {
        ProductionFlavour {
            //As of AS 3.0
            dimension "rooster-release"

            //For beta version: xxx 1 XXX XXX
            //For release version: xxx 0 XXX XXX
            //Warning: The greatest value Google Play allows for versionCode is 2100000000.
            //Formatting of version name very important for invalidation, must have 3 components
            //Beta version name is release version name at point of diversion, with alpha b

            //Release
            versionCode 40
            versionName "2.3.0"

            manifestPlaceholders = [
                    app_icon: "@mipmap/ic_launcher",
                    app_name: "Rooster",
                    fabric_api_key: "***REMOVED***",
            ]
        }
        BetaFlavour {
            //As of AS 3.0
            dimension "rooster-release"

            //Beta
            versionName "2.3.1b"
            versionCode 1000012

            manifestPlaceholders = [
                    app_icon: "@mipmap/ic_launcher_beta",
                    app_name: "Rooster Beta",
                    fabric_api_key: "***REMOVED***",
            ]
        }
    }

    buildTypes {
        release {
            //During debugging, enable debugging... and disable minify to get proper stacktraces
            //debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.txt'
            proguardFile 'proguard-square-okio.pro'
            proguardFile 'proguard-square-retrofit.pro'
            proguardFile 'proguard-square-retrofit2.pro'
            proguardFile 'proguard-square-dagger.pro'
            proguardFile 'proguard-square-okhttp.pro'
            proguardFile 'proguard-square-picasso.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
            signingConfig signingConfigs.release
        }
    }
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

def support_lib_version = "26.1.0"

def google_play_services_version = "11.4.2"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    androidTestImplementation 'junit:junit:4.12'

    androidTestImplementation "com.android.support:support-annotations:${support_lib_version}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    // Optional -- UI testing with Espresso
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // required if you want to use Mockito for unit tests
    testImplementation 'org.mockito:mockito-core:2.7.22'
    // required if you want to use Mockito for Android tests
    androidTestImplementation 'org.mockito:mockito-android:2.7.22'

    implementation "com.android.support:support-core-utils:${support_lib_version}"
    implementation "com.android.support:appcompat-v7:${support_lib_version}"
    implementation "com.android.support:design:${support_lib_version}"
    implementation "com.android.support:support-v4:${support_lib_version}"
    implementation "com.android.support:recyclerview-v7:${support_lib_version}"
    implementation "com.android.support:cardview-v7:${support_lib_version}"
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar')
    {
        transitive = true;
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.0@aar') {
        transitive = true;
    }
    implementation "com.google.firebase:firebase-storage:${google_play_services_version}"
    implementation "com.google.firebase:firebase-auth:${google_play_services_version}"
    implementation "com.google.firebase:firebase-database:${google_play_services_version}"
    implementation "com.google.firebase:firebase-messaging:${google_play_services_version}"
    implementation "com.google.firebase:firebase-crash:${google_play_services_version}"
    implementation "com.google.firebase:firebase-core:${google_play_services_version}"
    implementation 'ch.hsr:geohash:1.3.0'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'com.afollestad.material-dialogs:core:0.8.5.9'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.mobsandgeeks:android-saripaar:2.0.3'
    implementation 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    implementation 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    implementation 'com.squareup.retrofit2:converter-scalars:2.1.0'
    implementation 'com.facebook.android:facebook-android-sdk:4.25.0'
    implementation 'com.facebook.stetho:stetho:1.4.2'
    implementation 'com.facebook.stetho:stetho-okhttp:1.4.2'
    implementation "com.google.android.gms:play-services-auth:${google_play_services_version}"
    implementation "com.google.android.gms:play-services-location:${google_play_services_version}"
    implementation "com.google.android.gms:play-services-places:${google_play_services_version}"
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.squareup.okhttp:okhttp:2.7.2'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.google.android.exoplayer:exoplayer-core:r2.5.3'
    implementation 'com.google.android.exoplayer:exoplayer-ui:r2.5.3'
    implementation 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.5.3'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.android.exoplayer:extension-mediasession:r2.5.3'
    //Upgraded Dagger to work with Butterknife
    //https://github.com/JakeWharton/butterknife/issues/863
    implementation 'com.google.dagger:dagger:2.9'
    kapt 'com.google.dagger:dagger-compiler:2.9'
    implementation 'com.jakewharton:butterknife:8.8.0'
    kapt 'com.jakewharton:butterknife-compiler:8.8.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
